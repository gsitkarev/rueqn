#!/usr/bin/awk -f

BEGIN {
	# Operator precedence levels.
	P_TO = 0; P_FROM = 1;
	P_OVER = 10; P_SQRT = 11; P_SUP = 12; P_SUB = 13;
	P_VCENTER = 14;		# custom box centering operator
	P_SIZE = 21; P_BOLD = 22; P_ITALIC = 23; P_ROMAN = 24; P_FAT = 25;
	P_UP = 30; P_DOWN = 31; P_BACK = 32; P_FWD = 33;
	P_DOTDOT = 41; P_DOT = 42; P_HAT = 43; P_TILDE = 44; P_BAR = 45;
	P_UNDER = 46; P_VEC = 47; P_DYAD = 48;

#	line = "f(x) \"\\h'2p'\"= \"\\h'2p'\" a over { b \"\\h'2p'\" + \"\\h'2p'\" 1 over { gamma + int xdx }~~ }~~ ~~"
#	line = "dx over dy = x sub i sup k + y sub i sup n"
	line = line "S = { a sub 1 x sub 1 + a sub 2 x sub 2 + a sub 3 x sub 3 + ... + a sub n x sub n } over { a sub 1 + a sub 2 } ; ~~"
	line = line "B sub x sub c sup b \"\\h'2p'\" = \"\\h'2p'\" A sup x sup b sub c sup y ; ~~"
#	line = "F(x, y) = int2 f(x, y) dx dy"
#	line = "F(u, v) \"\\h'2p'\" = \"\\h'2p'\" int2 sub { - inf } sup inf f(x,y) e sup { -j2 pi (ux + vy) } dx dy } "
	line = line "F(u, v) \"\\h'2p'\" = \"\\h'2p'\" int from { - inf } to inf \"\\h'2p'\" int from { - inf } to inf f(x,y) \"\\h'2p'\" e sup { -j2 pi (ux+vy) } dx dy ; ~~~"
	line = line "x \"\\h'2p'\"=\"\\h'2p'\" { sum from i=1 to n w sub i x sub i } over { sum from i=1 to n w sub i } "
#	line = "abc \"abc def\" xyz"
#	line = "abc \"abcdef"
	pos = 1			# position in the input line
	next_word = ""		# next word in input line (internal only)
	next_pspace = ""	# spaces before next word in input line (internal)
	next_sspace = ""	# spaces after next word in input line (internal)
	preceding_space = ""	# spaces before word, available after getword()
	subsequent_space = ""	# spaces following word (attached to it), available after getword
	dnumber = 0		# troff definition number, used to generate register/string names
	subslvl = 0		# subscript level, 0 means main equation line

	# Space table holds troff replacement for space characters.
#	spacetab["~"] = "\\h'1n'"
	spacetab["~"] = "\\h'\\n[.ss]u*1m/36u'"
	spacetab["^"] = "\\h'\\n[.ss]u*1m/72u'"

	# Operator substitutions.
	# If any operator requires romanization, insert
	# the font change sequence in the substitution string.
	# Operator substitutions go literally to troff output,
	# you don't need to double quote them.
	opsubstab["+"] = "\\(pl"
	opsubstab["-"] = "\\(mi"
	opsubstab["="] = "\\(eq"
	opsubstab["/"] = "\\(f/"

	# Word substitutions processed recursively. This table is
	# filled by the user through `define' statement.
	# To prevent font escaping, enclose definition in double quotes.
	# Double quoted defintion will go literally to troff output.
	substab["int"] = "{ vcenter \"\\s+6\\[integral]\\s0\" }"
	substab["int2"] = "{ vcenter \"\\s+6\\[integral]\\[integral]\\s0\" }"
	substab["GAMMA"] = "\"\\[*G]\""
	substab["gamma"] = "\"\\[*g]\""
	substab["PI"] = "\"\\[*P]\""
	substab["pi"] = "\"\\[*p]\""
	substab["inf"] = "\"\\[if]\""
	substab["sum"] = "{ \"\\s+6\\[*S]\\s0\" }"

	print ".ps 10"
	print ".vs 12"
	print ".sp 2v"

	print ".ft I"

	# For each sub/superscript level:
	#
	# 0sup<LEVEL>
	#	superscript baseline shift
	# 0sub<LEVEL>
	#	subscript baseline shift
	# 0s<LEVEL>font
	#	font size
	# 0s<LEVEL>dmin
	#	minimum distance between sub- and sup- boxes to
	#	enable automatic compensation
	print ".nr 0sup1 7p\n" \
	      ".nr 0sup2 4p\n" \
	      ".nr 0sub1 0-2p\n" \
	      ".nr 0sub2 0-2p\n" \
	      ".nr 0s1font 8p\n" \
	      ".nr 0s2font 6p\n" \
	      ".nr 0s3font 4p\n" \
	      ".nr 0s1dmin 1p\n" \
	      ".nr 0s2dmin 1p"

	print boxlist()
}

# This function generates troff line with fraction,
# where numerator string is in `num' and denominator in `den'.
# Definitions are printed to standard output.
function deffrac(num, den,	n)
{
	n = dnumber
	print ".ds 0s"n" "num"\n" \
	      ".nr 0w"n" \\w'\\*[0s"n"]'\n" \
	      ".nr 0t"n" 0\\n[rst]\n" \
	      ".nr 0b"n" 0\\n[rsb]"
	n++
	print ".ds 0s"n" "den"\n" \
	      ".nr 0w"n" \\w'\\*[0s"n"]'\n" \
	      ".nr 0t"n" 0\\n[rst]\n" \
	      ".nr 0b"n" 0\\n[rsb]"
	n++
	print ".nr 0w"n" \\n[0w"n-2"]>?\\n[0w"n-1"]"
	dnumber += 3
	return "\\Z\\(EQ\\v'0+\\n[0b"n-2"]u-2p-26M'\\h'\\n[0w"n"]u-\\n[0w"n-2"]u/2u+2p'"num"\\(EQ" \
	       "\\Z\\(EQ\\v'\\n[0t"n-1"]u'\\h'\\n[0w"n"]u-\\n[0w"n-1"]u/2u+2p'"den"\\(EQ" \
	       "\\v'0-26M'\\D'l \\n[0w"n"]u+4p 0'\\v'26M'"
}

# This function generates troff line with subscript `subs'
# and superscript `sups'. Both are optional. Subscripting level
# is provided by the user by `lvl' starting from one.
# Defintions are printed to standard output.
function defsubsup(subs, sups, lvl,	res, n, nsub, nsup)
{
	n = dnumber

	# Macro strings 0fontsave and 0fontrestore save and restore
	# font size of the current environment.
	print ".ds 0fontsave"n" \\R'0f0save"n" \\\\n(.s'\\s0\\R'0f1save"n" \\\\n(.s'\\s0\n" \
	      ".ds 0fontrestore"n" \\s'\\\\n[0f1save"n"]'\\s'\\\\n[0f0save"n"]'"
	
	if (subs)
		subs = "\\*[0fontsave"n"]\\s'\\n[0s"lvl"font]u'" subs "\\*[0fontrestore"n"]"
	if (sups)
		sups = "\\*[0fontsave"n"]\\s'\\n[0s"lvl"font]u'" sups "\\*[0fontrestore"n"]"

	if (subs) {
		n++
		print ".nr 0w"n" 0\\w'"subs"'\n" \
		      ".nr 0u"n" 0\\n[0sub"lvl"]\n" \
		      ".nr 0t"n" 0\\n[rst]+\\n[0u"n"]\n" \
		      ".nr 0b"n" 0\\n[rsb]"
		nsub = n
	}
	if (sups) {
		n++
		print ".nr 0w"n" 0\\w'"sups"'\n" \
		      ".nr 0u"n" 0\\n[0sup"lvl"]\n" \
		      ".nr 0b"n" 0\\n[rsb]+\\n[0u"n"]\n" \
		      ".nr 0t"n" 0\\n[rst]"
		nsup = n
	}
	if (subs && sups) {
		# Correct the distance between sub- and superscript.
		# If the distance between the sub- and superscript boxes
		# is less then allowed minimum, compensate baseline position
		# evenly between them.
		print ".if (\\n[0b"nsup"]-\\n[0t"nsub"])<\\n[0s"lvl"dmin] \\{\\\n" \
		      ".    nr 0u"nsup" +((\\n[0s"lvl"dmin]-(\\n[0b"nsup"]-\\n[0t"nsub"]))/2u)\n" \
		      ".    nr 0u"nsub" -((\\n[0s"lvl"dmin]-(\\n[0b"nsup"]-\\n[0t"nsub"]))/2u)\n" \
		      ".\\}"
	}
	
	if (subs)
		res = res "\\Z\\(EQ\\v'0-\\n[0u"nsub"]u'"subs"\\(EQ" \
		      "\\v'0-(\\n[0u"nsub"]u+\\n[0b"nsub"]u'\\&" \
		      "\\v'\\n[0u"nsub"]u+\\n[0b"nsub"]u'"
	if (sups)
		res = res "\\Z\\(EQ\\v'-\\n[0u"nsup"]u'"sups"\\(EQ" \
		      "\\v'-(\\n[0u"nsup"]u+\\n[0t"nsup"]u)'\\&" \
		      "\\v'\\n[0u"nsup"]u+\\n[0t"nsup"]u'"
	dnumber = n
	return res "\\h'\\w'"subs"'u>?\\w'"sups"'u'"
}

# This function returns troff vbox
# with limits centered below (from) and above (to) it.
# Spaces are added on both sides if the limits are wider than the box itself.
# Definitions are printed to standard output.
function deflimits(vbox, from, to,		res, n)
{
	n = dnumber

	print ".nr 0w"n" \\w'"vbox"'\n" \
	      ".nr 0t"n" 0\\n[rst]\n" \
	      ".nr 0b"n" 0\\n[rsb]\n" \
	      ".nr 0s"n" (\\n[0w"n"]>?\\w'"from"'>?\\w'"to"')-\\n[0w"n"]"

	# Limits are spaced by 1 point from the box.
	if (from) {
		n++
		print ".nr 0w"n" 0\\w'"from"'\n" \
		      ".nr 0t"n" 0\\n[rst]\n" \
		      ".nr 0b"n" 0\\n[rsb]"
		res = "\\Z\\(EQ" \
		      "\\h'-(\\n[0w"dnumber"]u+\\n[0w"n"]u)/2u'" \
		      "\\v'0\\n[0t"n"]u-\\n[0b"dnumber"]u+1p'" \
		      from "\\(EQ"
	}
	if (to) {
		n++
		print ".nr 0w"n" \\w'"to"'\n" \
		      ".nr 0t"n" 0\\n[rst]\n" \
		      ".nr 0b"n" 0\\n[rsb]"
		res = res "\\Z\\(EQ" \
		      "\\h'-(\\n[0w"dnumber"]u+\\n[0w"n"]u)/2u'" \
		      "\\v'-(\\n[0t"dnumber"]u-\\n[0b"n"]u+1p'" \
		      to "\\(EQ"
	}
	res = "\\h'\\n[0s"dnumber"]u/2u'" vbox res "\\h'\\n[0s"dnumber"]u/2u'"
	dnumber = n+1
	return res
}

# This function returns troff vbox centered vertically against the main formula line.
function defvcenter(vbox,		n, res)
{
	n = dnumber

	print ".nr 0w"n" \\w'"vbox"'\n" \
	      ".nr 0t"n" 0\\n[rst]\n" \
	      ".nr 0b"n" 0\\n[rsb]"
	res = "\\v'(\\n[0t"n"]u+\\n[0b"n"]u/2u-28M)'" \
	      vbox \
	      "\\v'-(\\n[0t"n"]u+\\n[0b"n"]u/2u-28M)'"
	dnumber += 1
	return res
}

function boxlist(	w, res)
{
	while ((w = nextword()) && w != "}")
		res = res box("", -1)
	return res
}

# Prints the message to standard error and exit with non-zero code.
function errx(msg)
{
	printf "rueqn[%u]: near position %d: %s\n", PROCINFO["pid"], pos, msg >"/dev/stderr"
	exit 1
}

function box(leftside, minprec,		w, res, numbox, denbox, subbox, supbox, frombox, tobox)
{
	if (nextword() == "vcenter" && minprec <= V_CENTER) {
		getword()
		res = leftside preceding_space defvcenter(box("", V_CENTER))
	} else if (nextword() == "sup" && minprec <= P_SUP) {
		if (!leftside)
			errx("missing box for `sup'")
		getword()
		if (minprec != P_SUB)
			subslvl++
		supbox = box("", P_SUP)
		res = leftside preceding_space defsubsup("", supbox, subslvl)
		if (minprec != P_SUB)
			subslvl--
	} else if (nextword() == "sub" && minprec <= P_SUB) {
		if (!leftside)
			errx("missing box for `sub'")
		getword()
		leftside = leftside preceding_space
		subslvl++
		subbox = box("", P_SUB)
		if (nextword() == "sup") {
			getword()
			supbox = preceding_space box("", P_SUP)
		}
		res = leftside defsubsup(subbox, supbox, subslvl)
		subslvl--
	} else if (nextword() == "to" && minprec <= P_TO) {
		if (!leftside)
			errx("missing box to apply `to' to")
		getword()
		tobox = box("", P_TO)
		leftside = leftside preceding_space
		res = deflimits(leftside, "", tobox)
	} else if (nextword() == "from" && minprec <= P_FROM) {
		if (!leftside)
			errx("missing box to apply `from' to")
		getword()
		leftside = leftside preceding_space
		frombox = box("", P_FROM)
		if (nextword() == "to") {
			getword()
			tobox = preceding_space box("", P_TO)
		}
		res = deflimits(leftside, frombox, tobox)
	} else if (nextword() == "over" && minprec < P_OVER) {
		if (!leftside)
			errx("missing left side for `over'")
		# Operator `over' associates to the left.
		# We replace the left side with the fraction and proceed
		# with the operators.
		getword()
		numbox = leftside preceding_space
		denbox = box("", P_OVER)
		res = box(deffrac(numbox, denbox), -1)
	} else {
		if (leftside) {
			# No operators can be applied to the leftside.
			# The result of this box is the leftside itself.
			res = leftside
		} else {
			# No leftside yet, get the word and use it
			# as a leftside for the box recursively.
			if (nextword() == "{") {
				getword()
				leftside = preceding_space boxlist()
				if (nextword() != "}")
					errx("missing closing `}'")
				getword()
			} else {
				w = getword()
				leftside = preceding_space fontescape(w) subsequent_space
			}
			res = box(leftside, minprec)
		}
	}

	return res
}

function chr(line, pos)
{
	return substr(line, pos, 1)
}

function nextword(	space)
{
	if (!next_word) {
		next_word = real_getword(space)
		next_pspace = space[0]
		next_sspace = space[1]
	}
	return next_word
}

function getword(	res, space)
{
	if (next_word) {
		res = next_word
		preceding_space = next_pspace
		subsequent_space = next_sspace
		next_word = ""
	} else {
		res = real_getword(space)
		preceding_space = space[0]
		subsequent_space = space[1]
	}
	return res
}

# This function romanizes parenthesis, digits and operators.
# Variables are italic corrected before and after.
# Double-quoted blocks (troff literal blocks) are skipped.
# Operator substitutions are looked up in `opsubstab' table.
function fontescape(word,	c, i, res)
{
	for (i = 1; c = chr(word, i); i++) {
		if (c == "\"") {
			# Here we skip the literal part.
			for (++i; (c = chr(word, i)) && c != "\""; i++) {
				res = res c
			}
			continue
		}
		if (c in opsubstab) {
			res = res opsubstab[c]
		} else if (c ~ /[0-9]/) {
			res = res "\\fR\\," c "\\/\\fP"
		} else if (c ~ /[a-zA-Z]/) {
			res = res "\\," c
			while ((c = chr(word, i+1)) ~ /[a-zA-Z]/) {
				res = res c
				i++
			}
			res = res "\\/"
		} else if (c ~ /[][(){}]/) {
			res = res "\\fR\\," c "\\/\\fP"
		} else
			res = res c
	}
	return res
}

# helper replaces the word with its substitution at current word position
function dosubst(w)
{
	if (w in substab) {
		pos -= length(w)
		line = substr(line, 1, pos-1) substab[w] substr(line, pos+length(w))
		return 1
	}
	return 0
}

# This function extracts the next word from the line and recursively substitutes it.
function real_getword(space,	c, res)
{
	space[0] = space[1] = ""

	# FIXME:
	# Insert reasonable substitution recursion limit to prevent endless loops.
	#
	do {
		res = ""
		# skip delimiting spaces and collect preceeding spaces
		for ( ; (c = chr(line, pos)) && c ~ /[ ^~]/; pos++) {
			if (c ~ /[~^]/)
				space[0] = space[0] spacetab[c]
		}
		for ( ; (c = chr(line, pos)) && c !~ /[ ^~]/; pos++) {
			if (c == "\"") {
				# Literal blocks last till closing double quote.
				res = res "\""
				for (++pos; (c = chr(line, pos)) && c != "\""; pos++)
					res = res c
				if (c == "\"") {
					res = res c
					continue
				}
			} else
				res = res c
		}
		# collect subsequent spaces
		for ( ; (c = chr(line, pos)) && c ~ /[~^]/; pos++)
			space[1] = space[1] spacetab[c]
	} while (dosubst(res))

	# print space[0] " " res " " space[1] >"/dev/stderr"

	return res
}

